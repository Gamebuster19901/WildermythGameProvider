plugins {
	id 'java-library'
	id 'eclipse'
	id 'maven-publish'
	id ('com.gradleup.shadow') version "8.3.3"
}

repositories {
	mavenCentral()
	maven {
		url "https://repo.spongepowered.org/maven/"
	}
	maven {
		url "https://maven.fabricmc.net/"
	}
	mavenLocal()
}

sourceCompatibility = '17'
targetCompatibility = '17'

version = providerVersion
group = "com.wildermods"
archivesBaseName = "provider"

dependencies {

	compileOnly group: 'net.fabricmc', name: 'fabric-loader', version: fabricVersion

	//mixin requirements:
	compileOnly group: 'net.fabricmc', name: 'sponge-mixin', version: mixinVersion
	
	compileOnly group: 'org.ow2.asm', name: 'asm', version: asmVersion
	compileOnly group: 'org.ow2.asm', name: 'asm-analysis', version: asmVersion
	compileOnly group: 'org.ow2.asm', name: 'asm-commons', version: asmVersion
	compileOnly group: 'org.ow2.asm', name: 'asm-tree', version: asmVersion
	compileOnly group: 'org.ow2.asm', name: 'asm-util', version: asmVersion
	
	implementation(group: 'org.apache.commons', name: 'commons-lang3', version: commonsLangVersion) {
		transitive = false
	}
}

jar {
	manifest {
		attributes(
			'Main-Class': 'net.fabricmc.loader.impl.launch.knot.KnotClient',
			'Class-Path': configurations.runtimeClasspath.collect { it.getName() }.join(' '),
			'Multi-Release': 'true'
		)
	}
}

task sourcesJar(type: Jar) {
	archiveClassifier.set("sources")
	from sourceSets.main.allSource
}

tasks.build {
	dependsOn sourcesJar
}

tasks.named('shadowJar', com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
	exclude '**/license.txt'
	exclude '**/license'
	exclude '**/notice.txt'
	exclude '**/notice' 
	finalizedBy sourcesJar
}

artifacts {
	archives jar
	archives sourcesJar
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			artifact sourcesJar
		}
	}
}